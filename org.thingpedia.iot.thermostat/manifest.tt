abstract class @org.thingpedia.iot.thermostat
#_[thingpedia_name="Thermostat"]
#_[thingpedia_description="Interface for Thermostat."]
#_[description="Interface for Thermostat."]
#[license="BSD-3-Clause"]
#[license_gplcompatible=true]
#[subcategory="home"]
{
  monitorable query humidity(out state : Enum(humid,normal), out value: Number)
  #_[canonical="get humidity"]
  #_[confirmation="the humidity from $__device"]
  #_[confirmation_remote="the humidity reported by $__person's $__device"]
  #_[formatted=["It is ${state}.", "The humidity level is ${value} percent."]]
  #[poll_interval=0ms]
  #[doc="get the current humidity (in percent)"];

  monitorable query hvac_state(out mode: Enum(heat,cool,heat_cool,off,auto,dry,fan_only),
                                   out state: Enum(heating,cooling,off,drying,idle,fan))
  #_[canonical="get hvac state on thermostat"]
  #_[confirmation="the state of your thermostat"]
  #_[confirmation_remote="the state of $__person's $__device"]
  #_[formatted=[{type="text",text="Your thermostat is ${state}."}]]
  #[poll_interval=0ms]
  #[doc="get the state of the heating system"];

  monitorable query temperature(out value: Measure(C))
  #_[canonical="get temperature"]
  #_[confirmation="the temperature from $__device"]
  #_[confirmation_remote="the temperature on $__person's $__device"]
  #_[formatted=[{type="text",text="The temperature is ${value:C}Â°C"}]]
  #[poll_interval=0ms]
  #[doc="get the current temperature"];

  action set_hvac_mode(in req mode: Enum(heat,cool,heat_cool,off) #_[prompt="What mode do you want to set?"])
  #_[canonical="set hvac mode on thermostat"]
  #_[confirmation="set your $__device to $mode"]
  #_[confirmation_remote="set your $__device to $mode"]
  #[doc="change the mode of the HVAC system associated with this thermostat"];

  action set_minmax_temperature(in req low: Measure(C) #_[prompt="What low temperature do you want to set?"],
                                in req high: Measure(C) #_[prompt="What high temperature do you want to set?"])
  #_[canonical="set minimum maximum temperature on thermostat"]
  #_[confirmation="set your $__device between $low and $high"]
  #_[confirmation_remote="set your $__device between $low and $high"]
  #[doc="set desired temperature range"];

  action set_target_temperature(in req value: Measure(C) #_[prompt="What temperature do you want to set?"])
  #_[canonical="set target temperature on thermostat"]
  #_[confirmation="set your $__device to $value"]
  #_[confirmation_remote="set your $__device to $value"]
  #[doc="set target temperature"];
}
